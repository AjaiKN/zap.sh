#compdef trash
#description Move a file to the trash

local -a strategies
strategies=(
	'freedesktop:Use freedesktop.org Trash Specification (default on Linux)'
	'macos_trash_command:Use /usr/bin/trash (default on macOS >=14.0)'
	'macos_applescript:Use AppleScript (default on macOS <14.0)'
	'macos_mv:Use mv to move to ~/.Trash/'
	'gio:Use the "gio trash" command'
	'kioclient:Use "kioclient move FILE... trash:/"'
	'trash_cli:Use trash-put from trash-cli'
	'dangerous_rm:Use rm (dangerous)'
	'dangerous_shred:Use GNU shred (dangerous)'
)

_arguments -s \
	'(-h --help)'{-h,--help}'[Show this usage message]' \
	'(-f --force)'{-f,--force}'[Override non-writable files, and ignore nonexistent or otherwise untrashable files]' \
	'(-n --dry-run)'{-n,--dry-run}'[Don'\''t actually trash anything; implies --verbose]' \
	'(-v --verbose)'{-v,--verbose}'[Be more verbose]' \
	'(-s --strategy=)'{-s,--strategy=}'[Specify which trashing strategy to use]:strategy:(( $strategies ))' \
	'(-d --dir)'{-d,--dir}'[Does nothing. For compatibility with rm]' \
	'(-r -R --recursive)'{-r,-R,--recursive}'[Does nothing. For compatibility with rm]' \
	'*:file:_files'
